@synth voxhumana
longname : Vox Humana
type : synth
author : Guy Brown
version : 1.0
doc : Polymoog Vox Humana patch as used by Gary Numan
@end

@param release
type: float
mutable: no
step: 0.01
min: 0
max: 1
default: 0.2
doc: the length of the envelope release
@end

@param volume
type: float
mutable: yes
step: 0.01
min: 0
max: 1
default: 0.5
doc: overall volume
@end

# modules

SAW-OSC : vco1
PULSE-OSC : vco2
LPF : filter
VCA : mix
VCA : vca
ADSR : env
TRI-OSC : lfo1
TRI-OSC : lfo2
TRI-OSC : lfo3
VCA : gain1
VCA : gain2
VCA : gain3

# patching

vco1.out -> mix.in
vco2.out -> mix.in
mix.out -> filter.in
filter.out -> vca.in
vca.out -> audio.in

# lfo1 modulates the pitch of vco1

lfo1.out -> gain1.in
gain1.out -> vco1.pitchCV
lfo1.pitch = 5.04
gain1.level = 5

# lfo2 modulates the pitch of vco2

lfo2.out -> gain2.in
gain2.out -> vco2.pitchCV
lfo2.pitch = 3.5
gain2.level = 5

# lfo3 modulates the pulse width of vco2

lfo3.out -> gain3.in
lfo3.pitch = 5.72
gain3.level = 0.2
gain3.out -> vco2.pulsewidthCV
vco2.pulsewidth = 0.5

# filter parameters

filter.cutoff = map(param.pitch,2000,15000)
filter.resonance = 0

# envelope

env.out -> vca.levelCV
env.attack = 0.453
env.decay = 0
env.level = param.level*param.volume
env.sustain = param.level*param.volume
env.release = map(param.release,0.5,2.5)

# detune oscillator 2 a bit

vco2.detune = -5

# set the mix to half since we are combining two oscillators

mix.level = 0.6

# keyboard inputs

vco1.pitch = param.pitch
vco2.pitch = param.pitch

# end
