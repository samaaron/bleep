@synth sawlead
longname : Classic saw lead
type : synth
author : Guy Brown
version : 1.0
doc : Simple saw lead with two detuned oscillators
@end

@param cutoff
type : float
mutable: yes
step : 0.01
min : 0
max : 1
default : 0.9
doc : filter cutoff
@end

@param detune
type : float
mutable: yes
step : 0.01
min : 0
max : 10
default : 4
doc : amount of oscillator detune
@end

@param filter_mod
type : float
mutable: no
step : 0.01
min : 0
max : 1
default : 0.5
doc : amount of filter envelope modulation
@end

@param filter_attack
type : float
mutable: no
step : 0.01
min : 0
max : 1
default : 0.1
doc : length of filter envelope attack
@end

@param filter_decay
type : float
mutable: no
step : 0.01
min : 0
max : 2
default : 0.8
doc : length of filter envelope decay
@end

@param volume
type : float
mutable: no
step : 0.01
min : 0
max : 1
default : 0.8
doc : overall volume
@end

# modules

SAW-OSC : saw1
SAW-OSC : saw2
VCA : mix
LPF : lowpass
ADSR : filterenv
ADSR : ampenv
VCA : vca
VCA : wet

saw1.out -> mix.in
saw2.out -> mix.in
mix.out -> lowpass.in
lowpass.out -> vca.in

filterenv.out -> lowpass.cutoffCV

filterenv.level = map(param.cutoff,50,8000)
filterenv.attack = param.filter_attack
filterenv.decay = param.filter_decay
filterenv.sustain = map(param.cutoff,50,8000)*(1-param.filter_mod)
filterenv.release = 0.1

saw1.pitch = param.pitch
saw2.pitch = param.pitch
saw1.detune = -1*param.detune
saw2.detune = param.detune

ampenv.out -> vca.levelCV

ampenv.attack = 0.01
ampenv.release = 0.01
ampenv.level = param.level*param.volume
ampenv.decay = 0.1
ampenv.sustain = param.level*param.volume

# delay effect

DELAY : leftlag
DELAY : rightlag
PAN : left
PAN : right
VCA : stereomix
VCA : leftfb
VCA : rightfb

vca.out -> wet.in

wet.out -> leftlag.in
leftlag.out -> left.in
leftlag.out -> leftfb.in
leftfb.out -> leftlag.in
left.out -> stereomix.in

wet.out -> rightlag.in
rightlag.out -> right.in
rightlag.out -> rightfb.in
rightfb.out -> rightlag.in
right.out -> stereomix.in

vca.out -> stereomix.in

left.angle = -1
right.angle = 1

leftlag.lag = 0.75
rightlag.lag = 1.25

stereomix.out -> audio.in

wet.level = 0.2
leftfb.level = 0.3
rightfb.level = 0.3

# end


