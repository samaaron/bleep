@synth funkybass
longname : Funky Bass
type : synth
author : Guy Brown
version : 1.0
doc : Funky bass sound from the 1980s
@end

@param cutoff
type: float
mutable: no
step: 0.01
min: 0
max: 1
default : 0.3
doc : controls the brightness
@end

@param noise
type: float
mutable: yes
step: 0.01
min: 0
max: 1
default : 0.1
doc : controls the amount of noise
@end

@param resonance
type: float
mutable: yes
step: 0.01
min: 0
max: 1
default : 0.5
doc : controls the resonance of the filter
@end

@param accent
type: float
mutable: no
step: 0.01
min: 0
max: 1
default : 0.7
doc : controls the level of the attack transient
@end

@param volume
type: float
mutable: no
step: 0.01
min: 0
max: 1
default : 0.7
doc : controls the volume
@end

# modules

PULSE-OSC : pwm
SAW-OSC : saw
SQR-OSC : sub
NOISE : noise
VCA : mix1
VCA : mix2
VCA : mix3
VCA : mix4
VCA : mixer
VCA : vca
ADSR : env1
ADSR : env2
LPF : vcf

pwm.out -> mix1.in
saw.out -> mix2.in
sub.out -> mix3.in
noise.out -> mix4.in
mix1.level = 0.4
mix2.level = 0.4
mix3.level = 0.2
mix4.level = param.noise

mix1.out -> mixer.in
mix2.out -> mixer.in
mix3.out -> mixer.in
mix4.out -> mixer.in

env1.out -> vca.levelCV
env2.out -> vcf.cutoffCV

pwm.pitch = param.pitch
saw.pitch = param.pitch
sub.pitch = param.pitch
sub.detune = -1200

mixer.out -> vcf.in
vcf.out -> vca.in
vca.out -> audio.in
vcf.resonance = map(param.resonance,0,8)

env1.level = param.level*param.volume
env1.attack = 0.001
env1.decay = 0.5
env1.sustain = param.level*param.volume
env1.release = 0.1

env2.level = map(param.accent,400,4000)
env2.attack = 0.001
env2.decay = 0.03
env2.sustain = map(param.cutoff,250,800)
env2.release = 0.1

pwm.pulsewidth = 0.56

# end


