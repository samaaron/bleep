@synth dogbass
longname : TBD bass sound
type : synth
author : Guy Brown
version : 1.0
doc : TBD bass sound a bit thinner than a square
@end

@param cutoff
type: float
mutable: yes
step: 0.01
min: 100
max: 8000
default: 700
doc : Controls the brightness of the sound
@end

@param volume
type: float
mutable: yes
step: 0.01
min: 0
max: 1
default: 1
doc : Controls the overall volume
@end

# this is a pulse oscillator high pass filtered
# and add a sine as a sub oscillator

PULSE-OSC : vco
VCA : vca
VCA : volume
LPF : lowpass
ADSR : env
ADSR : filtenv

# connect them

vco.out -> lowpass.in
lowpass.out ->  vca.in
vca.out -> volume.in
volume.out -> audio.in
env.out -> vca.levelCV
vca.level=0.5

# tweaks

vco.pulsewidth=0.6
vco.pitch = param.pitch
env.level = param.level*param.volume
env.attack = 0.01
env.release = 0.15
env.sustain = 0
env.decay = 0.5

filtenv.level = param.cutoff
filtenv.attack = 0.01
filtenv.release = 0.15
filtenv.sustain = param.cutoff*0.2
filtenv.decay = 0.2

filtenv.out -> lowpass.cutoffCV

# end


